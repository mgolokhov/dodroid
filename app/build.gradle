buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.4'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


ext.versions = [
        // Android
        targetSdkVersion    : 28,
        minSdkVersion       : 21,
        buildtools          : "29.0.2",
        retrofit            : '2.6.2'
]


def versionMajor = 0
def versionMinor = 5
def versionBuild = 30

android {
    compileSdkVersion versions.targetSdkVersion
    buildToolsVersion versions.buildtools
    defaultConfig {
        applicationId "doit.study.droid"
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        versionCode versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionBuild}"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    // UI
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.mikhaellopez:circularprogressbar:3.0.3'
    // Network: Retrofit & OkHttp
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofit"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.2'
    // Analytics
    implementation 'com.google.android.gms:play-services-analytics:17.0.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') {
        transitive = true
    }
    def coroutines_ver = '1.3.2'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_ver"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_ver"

    implementation 'androidx.core:core-ktx:1.1.0'
}


task exportQuestions(type: Exec, description: 'Run the pre-build python script') {
    workingDir 'src/main/res/raw/'
    commandLine 'python', 'import_quiz_to_json.py'
}

// This ensures my script will be called before building
// preBuild.dependsOn exportQuestions
